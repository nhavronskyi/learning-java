plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.11.4'
    lombokVersion = '1.18.36'
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
    useJUnitPlatform()
}

dependencyUpdates {
    doLast {
        def reportFile = file("${layout.buildDirectory.get().asFile}/dependencyUpdates/report.txt")
        if (reportFile.exists() && reportFile.text.contains("The following dependencies have later milestone versions:")) {
            def reportContent = reportFile.text
            def dependenciesSection = reportContent.split("The following dependencies have later milestone versions:")[1]

            def relevantLines = dependenciesSection.split("Gradle release-candidate updates:")[0]

            relevantLines.eachLine { line ->
                if (line.startsWith(" - ")) {
                    logger.lifecycle("WARNING: $line")
                }
            }
        }
    }
}
